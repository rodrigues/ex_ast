defmodule ExAST.PatternTest do
  use ExUnit.Case, async: true

  alias ExAST.Patcher
  alias ExAST.Pattern

  defp match!(source, pattern) do
    ast = Sourceror.parse_string!(source)
    Pattern.match(ast, pattern, Pattern.collect_aliases(ast))
  end

  describe "literals" do
    test "exact match" do
      assert {:ok, %{}} = match!("IO.inspect(data)", "IO.inspect(data)")
    end

    test "atom" do
      assert {:ok, %{}} = match!(":ok", ":ok")
      assert :error = match!(":ok", ":error")
    end

    test "string" do
      assert {:ok, %{}} = match!(~s("hello"), ~s("hello"))
      assert :error = match!(~s("hello"), ~s("world"))
    end

    test "integer" do
      assert {:ok, %{}} = match!("42", "42")
      assert :error = match!("42", "43")
    end

    test "no match on different function" do
      assert :error = match!("IO.inspect(data)", "IO.puts(data)")
    end

    test "no match on different arity" do
      assert :error = match!("Enum.map(a, b)", "Enum.map(a)")
    end
  end

  describe "wildcards" do
    test "underscore matches anything" do
      assert {:ok, %{}} = match!("IO.inspect(data)", "IO.inspect(_)")
    end

    test "underscore-prefixed matches anything" do
      assert {:ok, %{}} = match!("IO.inspect(data)", "IO.inspect(_expr)")
    end

    test "wildcards don't appear in captures" do
      assert {:ok, caps} = match!("Enum.map(list, fun)", "Enum.map(_, _)")
      assert caps == %{}
    end
  end

  describe "captures" do
    test "single capture" do
      assert {:ok, caps} = match!("IO.inspect(data)", "IO.inspect(expr)")
      assert Map.has_key?(caps, :expr)
    end

    test "multiple captures" do
      assert {:ok, caps} = match!("Enum.map(list, fun)", "Enum.map(input, mapper)")
      assert Map.has_key?(caps, :input)
      assert Map.has_key?(caps, :mapper)
    end

    test "repeated variable requires same value" do
      assert {:ok, _} = match!("Enum.map(x, x)", "Enum.map(a, a)")
      assert :error = match!("Enum.map(x, y)", "Enum.map(a, a)")
    end

    test "capture string value" do
      assert {:ok, %{name: "subject"}} =
               match!(~s(%Step{id: "subject"}), ~s(%Step{id: name}))
    end
  end

  describe "structs (partial match)" do
    test "matches with subset of keys" do
      assert {:ok, %{}} =
               match!(
                 ~s(%Step{id: "subject", title: "Hello", fields: []}),
                 ~s(%Step{id: "subject"})
               )
    end

    test "captures struct field values" do
      assert {:ok, %{name: "subject"}} =
               match!(
                 ~s(%Step{id: "subject", title: "Hello"}),
                 ~s(%Step{id: name})
               )
    end

    test "rejects missing key" do
      assert :error =
               match!(
                 ~s(%Step{id: "subject"}),
                 ~s(%Step{id: "subject", nonexistent: _})
               )
    end

    test "rejects wrong struct name" do
      assert :error = match!(~s(%Step{id: "x"}), ~s(%Field{id: "x"}))
    end

    test "matches with multiple pattern keys" do
      assert {:ok, %{}} =
               match!(
                 ~s(%Step{id: "subject", title: "Hello", fields: []}),
                 ~s(%Step{id: "subject", title: "Hello"})
               )
    end
  end

  describe "maps (partial match)" do
    test "matches with subset of keys" do
      assert {:ok, %{}} =
               match!(
                 ~s(%{name: "John", age: 30}),
                 ~s(%{name: "John"})
               )
    end

    test "captures map values" do
      assert {:ok, %{n: "John"}} =
               match!(
                 ~s(%{name: "John", age: 30}),
                 ~s(%{name: n})
               )
    end
  end

  describe "tuples" do
    test "{:ok, _}" do
      assert {:ok, %{}} = match!("{:ok, 42}", "{:ok, _}")
    end

    test "{:error, reason}" do
      assert {:ok, %{reason: "boom"}} =
               match!(~s({:error, "boom"}), "{:error, reason}")
    end

    test "{:noreply, state}" do
      assert {:ok, caps} = match!("{:noreply, []}", "{:noreply, state}")
      assert Map.has_key?(caps, :state)
    end

    test "quoted source 2-tuple against quoted pattern" do
      assert {:ok, %{}} = Pattern.match(quote(do: {:ok, value}), quote(do: {:ok, _}))

      assert {:ok, %{v: _}} = Pattern.match(quote(do: {:ok, value}), quote(do: {:ok, v}))

      assert :error = Pattern.match(quote(do: :error), quote(do: {:ok, _}))
    end
  end

  describe "tuple ellipsis via find_all (all arities)" do
    defp find(source, pattern), do: Patcher.find_all(source, pattern)

    test "{...} matches every tuple arity" do
      assert [_] = find("x = {}", "{...}")
      assert [_] = find("x = {:only}", "{...}")
      assert [_] = find("x = {:ok, value}", "{...}")
      assert [_] = find("x = {1, 2, 3}", "{...}")
      assert [_] = find("x = {1, 2, 3, 4}", "{...}")
    end

    test "{...} does not match non-tuples" do
      assert [] = find("x = [1, 2, 3]", "{...}")
      assert [] = find("x = %{a: 1}", "{...}")
    end

    test "{:ok, ...} matches 2-tuples specifically" do
      assert [_] = find("x = {:ok, value}", "{:ok, ...}")
      assert [_] = find("x = {:ok, 1, 2}", "{:ok, ...}")
      assert [] = find("x = {:error, reason}", "{:ok, ...}")
    end

    test "{..., :done} matches 2-tuples specifically" do
      assert [_] = find("x = {:step, :done}", "{..., :done}")
      assert [_] = find("x = {1, 2, :done}", "{..., :done}")
      assert [] = find("x = {:step, :pending}", "{..., :done}")
    end

    test "captures still work on 2-tuples" do
      assert [%{captures: caps}] = find("x = {:ok, payload}", "{:ok, v}")
      assert Map.has_key?(caps, :v)

      assert [%{captures: caps}] = find("x = {:ok, payload}", "{a, b}")
      assert Map.has_key?(caps, :a)
      assert Map.has_key?(caps, :b)

      assert [_] = find("x = {:ok, payload}", "{_, _}")
    end

    test "ellipsis captures head, tail, and both sides" do
      assert [%{captures: %{first: _}}] = find("x = {1, 2, 3}", "{first, ...}")
      assert [%{captures: %{last: _}}] = find("x = {1, 2, 3}", "{..., last}")

      assert [%{captures: caps}] = find("x = {1, 2, 3, 4}", "{first, ..., last}")
      assert Map.has_key?(caps, :first)
      assert Map.has_key?(caps, :last)
    end

    test "multiple fixed elements on either side of ellipsis" do
      assert [%{captures: caps}] = find("x = {1, 2, 3, 4}", "{a, b, ...}")
      assert Map.has_key?(caps, :a)
      assert Map.has_key?(caps, :b)

      assert [%{captures: caps}] = find("x = {1, 2, 3, 4}", "{..., c, d}")
      assert Map.has_key?(caps, :c)
      assert Map.has_key?(caps, :d)

      assert [_] = find("x = {1, 2, 3, 4, 5}", "{a, ..., d, e}")
    end

    test "ellipsis enforces the minimum arity of fixed elements" do
      assert [_] = find("x = {1, 2}", "{first, ..., last}")
      assert [] = find("x = {1}", "{first, ..., last}")

      assert [_] = find("x = {1, 2}", "{a, b, ...}")
      assert [] = find("x = {1}", "{a, b, ...}")
    end

    test "ellipsis still respects fixed literals around it" do
      assert [_] = find("x = {:ok, 1, 2, :done}", "{:ok, ..., :done}")
      assert [] = find("x = {:ok, 1, 2, :nope}", "{:ok, ..., :done}")
      assert [] = find("x = {:no, 1, 2, :done}", "{:ok, ..., :done}")
    end

    test "maps and keywords are unaffected" do
      assert [_] = find("x = %{a: 1}", "%{a: 1}")
      assert [%{captures: %{n: _}}] = find("x = %{a: 1}", "%{a: n}")

      assert [_ | _] = find("x = [a: 1]", "[a: 1]")
      assert [%{captures: %{v: _}} | _] = find("x = [a: 1]", "[a: v]")
    end
  end

  describe "structural patterns on = and <-" do
    test "tuple on LHS of = (match operator)" do
      assert {:ok, %{}} = match!("{:ok, val} = fetch(y)", "{:ok, _} = _")
    end

    test "tuple with capture on LHS of =" do
      assert {:ok, caps} = match!("{:ok, val} = fetch(y)", "{:ok, x} = _")
      assert Map.has_key?(caps, :x)
    end

    test "tuple as RHS value of =" do
      assert {:ok, %{}} = match!("result = {:ok, val}", "_ = {:ok, _}")
    end

    test "3-tuple on LHS of =" do
      assert {:ok, %{}} = match!("{:ok, a, b} = fetch(y)", "{:ok, _, _} = _")
    end

    test "list on LHS of =" do
      assert {:ok, %{}} = match!("[h | t] = fetch(y)", "[_ | _] = _")
    end

    test "map on LHS of =" do
      assert {:ok, %{}} = match!("%{a: v} = fetch(y)", "%{a: _} = _")
    end

    test "wildcard on LHS of =" do
      assert {:ok, %{}} = match!("{:ok, val} = fetch(y)", "_ = _")
    end

    test "capture on LHS of =" do
      assert {:ok, %{pat: _}} = match!("{:ok, val} = fetch(y)", "pat = _")
    end

    test "tuple on LHS of <- (with clause)" do
      assert {:ok, %{}} =
               match!(
                 "with {:ok, val} <- fetch(x) do\n  val\nend",
                 "with {:ok, _} <- _ do ... end"
               )
    end

    test "tuple with capture on LHS of <-" do
      assert {:ok, caps} =
               match!(
                 "with {:ok, val} <- fetch(x) do\n  val\nend",
                 "with {:ok, x} <- _ do ... end"
               )

      assert Map.has_key?(caps, :x)
    end

    test "find_all: tuple on LHS of = (match operator)" do
      source = """
      def g(y) do
        {:ok, val} = fetch(y)
        val
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "{:ok, _} = _")
    end

    test "find_all: tuple on LHS of <- (with clause)" do
      source = """
      def f(x) do
        with {:ok, val} <- fetch(x) do
          val
        end
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "with {:ok, _} <- _ do ... end")
    end

    test "find_all: tuple as RHS value of =" do
      source = """
      def g(y) do
        result = {:ok, y}
        result
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "_ = {:ok, _}")
    end

    test "tuple as a list element" do
      assert {:ok, %{}} = match!("[{:ok, val}]", "[{:ok, _}]")
    end

    test "tuple as a call argument inside a list" do
      assert {:ok, %{}} = match!("foo([{:ok, val}])", "foo([{:ok, _}])")
    end
  end

  describe "function definitions" do
    test "def with wildcards" do
      assert {:ok, %{}} =
               match!(
                 "def handle_call(:ping, _from, state)",
                 "def handle_call(_, _, _)"
               )
    end

    test "def with specific first arg" do
      assert {:ok, %{}} =
               match!(
                 "def handle_call(:ping, _from, state)",
                 "def handle_call(:ping, _, _)"
               )
    end

    test "wildcard _ matches any function name with args" do
      assert {:ok, %{}} = match!("defp helper(x), do: x + 1", "defp _(_), do: _")
    end

    test "underscore-prefixed name matches any function name with args" do
      assert {:ok, %{}} = match!("def run(x), do: x", "def _name(_), do: _")
    end

    test "wildcard with ellipsis matches any arity" do
      assert {:ok, %{}} = match!("defp other(a, b), do: a + b", "defp _(...) do ... end")
    end

    test "non-wildcard name does not match different function" do
      assert :error = match!("defp helper(x), do: x + 1", "defp other(_), do: _")
    end
  end

  describe "map and struct subset matching via search" do
    test "a plain map pattern matches any map containing that entry" do
      src = "one = %{a: 1}\ntwo = %{a: 1, b: 2}\n"
      assert [%{}, %{}] = ExAST.Patcher.find_all(src, "%{a: 1}")
    end

    test "%{..., k: v} matches any map containing that entry" do
      src = "one = %{a: 1}\ntwo = %{a: 1, b: 2}\n"
      assert [%{}, %{}] = ExAST.Patcher.find_all(src, "%{..., a: 1}")
    end

    test "%{..., k: capture} binds the value from a larger map" do
      assert [%{captures: %{v: 2}}] =
               ExAST.Patcher.find_all("x = %{a: 1, b: 2}", "%{..., b: v}")
    end

    test "a struct pattern matches structs of that type by a subset of fields" do
      assert [%{}] =
               ExAST.Patcher.find_all(
                 ~s|x = %Config{port: 4000, host: "y"}|,
                 "%Config{port: 4000}"
               )
    end
  end

  describe "map patterns with ellipsis" do
    test "%{...} matches any map without crashing on call-valued entries" do
      source = """
      defmodule M do
        def perms, do: %{admin: grant(:admin), user: :ok}
      end
      """

      assert [%{}] = ExAST.Patcher.find_all(source, "def name do %{...} end")
    end

    test "%{...} matches an empty map" do
      assert [%{}] = ExAST.Patcher.find_all("x = %{}", "%{...}")
    end

    test "%Struct{...} matches any struct of that type, including empty" do
      assert [%{}] = ExAST.Patcher.find_all(~s|x = %Config{port: 4000}|, "%Config{...}")
      assert [%{}] = ExAST.Patcher.find_all("x = %Config{}", "%Config{...}")
    end
  end

  describe "arity syntax in definitions" do
    defp find_defs(source, pattern) do
      ast = Sourceror.parse_string!(source)
      ExAST.Patcher.find_all(ast, pattern)
    end

    @fixture """
    defmodule M do
      def id, do: "x"
      def apply(a, b), do: %{a | k: b}
    end
    """

    test "def name/0 matches only zero-arity defs" do
      assert [%{captures: %{name: {:id, nil, nil}}}] =
               find_defs(@fixture, "def name/0 do ... end")
    end

    test "def name/2 matches only two-arity defs" do
      assert [%{captures: %{name: {:apply, nil, nil}}}] =
               find_defs(@fixture, "def name/2 do ... end")
    end

    test "def name/_ matches any arity and captures the name" do
      names =
        @fixture
        |> find_defs("def name/_ do ... end")
        |> Enum.map(fn %{captures: %{name: {n, nil, nil}}} -> n end)

      assert Enum.sort(names) == [:apply, :id]
    end

    test "def _/2 is a non-capturing arity constraint" do
      assert [%{captures: caps}] = find_defs(@fixture, "def _/2 do ... end")
      assert caps == %{}
    end

    test "empty-paren zero-arity source matches def name/0" do
      assert [%{captures: %{name: {:ping, nil, nil}}}] =
               find_defs("def ping(), do: :pong", "def name/0 do ... end")
    end

    test "arity syntax respects guards" do
      source = "def clamp(x) when x > 0, do: x"

      assert [%{captures: %{name: {:clamp, nil, nil}}}] =
               find_defs(source, "def name/1 do ... end")

      assert [] = find_defs(source, "def name/2 do ... end")
    end

    test "arity syntax matches defmacro heads" do
      assert [%{captures: %{name: {:m, nil, nil}}}] =
               find_defs("defmacro m(a, b), do: {a, b}", "defmacro name/2 do ... end")
    end

    test "arity syntax respects the definition kind" do
      source = "defp helper(x), do: x"

      assert [%{captures: %{name: {:helper, nil, nil}}}] =
               find_defs(source, "defp name/1 do ... end")

      assert [] = find_defs(source, "def name/1 do ... end")
    end

    test "with a body, arity syntax matches the same defs as wildcard args" do
      arity = @fixture |> find_defs("def name/2 do ... end") |> Enum.map(& &1.node)
      wildcard = @fixture |> find_defs("def _(_, _) do ... end") |> Enum.map(& &1.node)
      assert wildcard != []
      assert arity == wildcard
    end

    test "short form requires a body, just like the paren form" do
      assert [] = find_defs(@fixture, "def name/2")
      assert [] = find_defs(@fixture, "def _(_, _)")
    end

    test "bare short form matches a bodyless def, like the bare paren form" do
      source = "def foo(a, b)"
      assert [%{captures: %{name: {:foo, nil, nil}}}] = find_defs(source, "def name/2")
      assert [_] = find_defs(source, "def _(_, _)")
    end

    test "name capture supports repeated captures in the body" do
      assert [_] = find_defs("def foo(foo), do: foo", "def name/1 do name end")
      assert [] = find_defs("def foo(bar), do: bar", "def name/1 do name end")
    end
  end

  describe "wildcard callees" do
    test "_(...) matches any local call, not special forms" do
      source = """
      defmodule M do
        def run, do: grant(:admin)
      end
      """

      assert [%{node: {:grant, _, _}}] = ExAST.Patcher.find_all(source, "_(...)")
    end

    test "_(...) does not match map literals or definitions" do
      source = "def run, do: %{a: 1}"
      assert [] = ExAST.Patcher.find_all(source, "_(...)")
    end

    test "_(...) does not match remote calls" do
      assert [] = ExAST.Patcher.find_all("x = Foo.bar(1)", "_(...)")
    end

    test "_._(...) matches any remote call" do
      assert [%{}] = ExAST.Patcher.find_all("x = Foo.bar(1)", "_._(...)")
    end

    test "_.name(...) matches any-module call to that function" do
      source = "defmodule M do\n  def run, do: Foo.section() + Bar.section(:x)\nend\n"
      assert [_, _] = ExAST.Patcher.find_all(source, "_.section(...)")
      assert [] = ExAST.Patcher.find_all(source, "_.missing(...)")
    end

    test "_(...) matches a zero-arity local call" do
      assert [%{node: {:reset, _, []}}] =
               ExAST.Patcher.find_all("def run, do: reset()", "_(...)")
    end

    test "_(...) matches a piped local call" do
      assert [_ | _] = ExAST.Patcher.find_all("def run, do: x |> transform()", "_(...)")
    end

    test "_(...) does not match word operators" do
      assert [] = ExAST.Patcher.find_all("left and right", "_(...)")
      assert [] = ExAST.Patcher.find_all("left or right", "_(...)")
      assert [] = ExAST.Patcher.find_all("not value", "_(...)")
      assert [] = ExAST.Patcher.find_all("value in list", "_(...)")
    end
  end

  describe "pipes" do
    test "pipe into function" do
      assert {:ok, %{}} = match!("data |> Enum.map(fun)", "_ |> Enum.map(_)")
    end

    test "pipe matches unpipelined call" do
      assert {:ok, %{}} = match!("data |> Enum.map(fun)", "Enum.map(_, _)")
    end

    test "unpipelined call matches pipe pattern" do
      assert {:ok, %{}} = match!("Enum.map(data, fun)", "data |> Enum.map(fun)")
    end

    test "pipe captures" do
      assert {:ok, %{input: _, mapper: _}} =
               match!("data |> Enum.map(fun)", "Enum.map(input, mapper)")
    end

    test "multi-step pipe normalizes" do
      assert {:ok, %{}} =
               match!(
                 "data |> Enum.map(f) |> Enum.filter(g)",
                 "Enum.filter(Enum.map(data, f), g)"
               )
    end

    test "pipe into zero-arity" do
      assert {:ok, %{}} = match!("data |> Enum.to_list()", "Enum.to_list(_)")
    end

    test "pipe into bare function" do
      assert {:ok, %{}} = match!("data |> to_string", "to_string(_)")
    end
  end

  describe "directives" do
    test "use" do
      assert {:ok, %{mod: {:__aliases__, nil, [:GenServer]}}} =
               match!("use GenServer", "use mod")
    end

    test "expands aliases for nested remote calls" do
      assert {:ok, %{}} =
               match!(
                 """
                 defmodule Example do
                   alias AshPhoenix.Form

                   def run(form) do
                     Form.for_update(form, :update)
                   end
                 end
                 """,
                 """
                 defmodule Example do
                   alias AshPhoenix.Form

                   def run(form) do
                     AshPhoenix.Form.for_update(form, :update)
                   end
                 end
                 """
               )
    end

    test "expands aliases for selector and inside-style matching" do
      source = """
      defmodule Example do
        alias AshPhoenix.Form

        def run(form) do
          value =
            if ready?() do
              Form.for_update(form, :update)
            end

          value
        end
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "AshPhoenix.Form.for_update(_, _)")

      assert [_] =
               ExAST.Patcher.find_all(source, "AshPhoenix.Form.for_update(_, _)",
                 inside: "def _ do ... end"
               )

      assert [] = ExAST.Patcher.find_all(source, "Form.for_update(_, _)")
    end

    test "import" do
      assert {:ok, caps} = match!("import Ecto.Query", "import mod")
      assert Map.has_key?(caps, :mod)
    end

    test "import-aware remote call matching" do
      source = """
      import Ecto.Query, only: [from: 2]

      from(u in User, where: u.id == 1)
      """

      assert [_] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
    end

    test "import :only only expands the listed functions" do
      source = """
      import Ecto.Query, only: [from: 2]

      from(u in User, where: u.id == 1)
      where(query, [u], u.id == 1)
      """

      assert [_] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
      assert [] = ExAST.Patcher.find_all(source, "Ecto.Query.where(_, _, _)")
      assert [_] = ExAST.Patcher.find_all(source, "where(_, _, _)")
    end

    test "import :only expands every listed arity of the same function" do
      source = """
      import Ecto.Query, only: [from: 1, from: 2]

      from(u in User)
      from(u in User, where: u.id == 1)
      """

      assert [_] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_)")
      assert [_] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
    end

    test "import with options but no :only does not expand calls" do
      source = """
      import Ecto.Query, warn: false

      from(u in User, where: u.id == 1)
      """

      assert [] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
      assert [_] = ExAST.Patcher.find_all(source, "from(_, _)")
    end

    test "import only: :functions does not expand calls" do
      source = """
      import Ecto.Query, only: :functions

      from(u in User, where: u.id == 1)
      """

      assert [] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
      assert [_] = ExAST.Patcher.find_all(source, "from(_, _)")
    end

    test "malformed :only entries are ignored" do
      source = """
      import Ecto.Query, only: [:from, where: :two]

      from(u in User, where: u.id == 1)
      where(query, [u], u.id == 1)
      """

      assert [] = ExAST.Patcher.find_all(source, "Ecto.Query.from(_, _)")
      assert [] = ExAST.Patcher.find_all(source, "Ecto.Query.where(_, _, _)")
    end

    test "a bare import does not break matching of unrelated calls" do
      source = """
      defmodule Demo do
        import Enum

        def add(a, b, c) do
          String.upcase("x")
          a + b + c
        end
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "def add(_, _, _) do ... end")
      assert [_] = ExAST.Patcher.find_all(source, "def _(_, _, _) do ... end")
      assert [_] = ExAST.Patcher.find_all(source, "add(_, _, _)")
      assert [_] = ExAST.Patcher.find_all(source, "String.upcase(_)")
      assert [_] = ExAST.Patcher.find_all(source, "def _ do ... end")
    end

    test "a bare import does not break Logger.info matching (bug report repro)" do
      source = """
      defmodule Demo do
        import Enum
        require Logger
        def run, do: Logger.info("hi")
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "Logger.info(...)")
    end

    test "import :only expands calls on a raw quoted AST" do
      ast =
        quote do
          import Ecto.Query, only: [from: 2]
          from(u in User, where: u.id == 1)
        end

      assert [_] = ExAST.Patcher.find_all(ast, "Ecto.Query.from(_, _)")
    end

    test "expand_imports resolves a bare import to its real exports" do
      source = """
      import Enum

      map(list, &(&1 + 1))
      """

      assert [] = ExAST.Patcher.find_all(source, "Enum.map(_, _)")

      assert [_] =
               ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports resolves import :except to the complement" do
      source = """
      import Enum, except: [map: 2]

      map(list, &(&1 + 1))
      filter(list, &(&1 > 0))
      """

      assert [] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
      assert [_] = ExAST.Patcher.find_all(source, "Enum.filter(_, _)", expand_imports: true)
    end

    test "expand_imports does not expand locally defined functions" do
      source = """
      defmodule Demo do
        import Enum

        def map(a, b), do: a + b
        def run, do: map(1, 2)
      end
      """

      assert [] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports leaves unknown modules alone" do
      source = """
      import NotARealModule

      whatever(1, 2)
      """

      assert [_] = ExAST.Patcher.find_all(source, "whatever(_, _)", expand_imports: true)
    end

    test "expand_imports does not leak a bare import into a sibling module" do
      source = """
      defmodule A do
        import Enum

        def run(list), do: map(list, & &1)
      end

      defmodule B do
        def run(list), do: map(list, & &1)
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports applies an outer import to a nested module" do
      source = """
      defmodule A do
        import Enum

        defmodule B do
          def run(list), do: map(list, & &1)
        end
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports local shadowing is scoped to the import's module" do
      source = """
      defmodule A do
        def map(a, b), do: {a, b}
      end

      defmodule B do
        import Enum

        def run(list), do: map(list, & &1)
      end
      """

      assert [_] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports local shadowing in a nested module" do
      source = """
      defmodule A do
        import Enum

        defmodule B do
          def map(a, b), do: {a, b}

          def run(list), do: map(list, & &1)
        end
      end
      """

      assert [] = ExAST.Patcher.find_all(source, "Enum.map(_, _)", expand_imports: true)
    end

    test "expand_imports only: :functions expands functions but not macros" do
      source = """
      import ExAST.Query, only: :functions

      from(pattern)
      where(query, expr)
      """

      assert [_] = ExAST.Patcher.find_all(source, "ExAST.Query.from(_)", expand_imports: true)
      assert [] = ExAST.Patcher.find_all(source, "ExAST.Query.where(_, _)", expand_imports: true)
    end

    test "expand_imports only: :macros expands macros but not functions" do
      source = """
      import ExAST.Query, only: :macros

      from(pattern)
      where(query, expr)
      """

      assert [] = ExAST.Patcher.find_all(source, "ExAST.Query.from(_)", expand_imports: true)
      assert [_] = ExAST.Patcher.find_all(source, "ExAST.Query.where(_, _)", expand_imports: true)
    end

    test "alias" do
      assert {:ok, caps} = match!("alias MyApp.Accounts.User", "alias mod")
      assert Map.has_key?(caps, :mod)
    end

    test "collects grouped aliases" do
      ast =
        Sourceror.parse_string!("""
        defmodule Example do
          alias Phoenix.Socket.{Broadcast, Message, Reply}
        end
        """)

      assert ExAST.Pattern.collect_aliases(ast) == %{
               Broadcast: [:Phoenix, :Socket, :Broadcast],
               Message: [:Phoenix, :Socket, :Message],
               Reply: [:Phoenix, :Socket, :Reply]
             }
    end
  end

  describe "module attributes" do
    test "@behaviour" do
      assert {:ok, caps} = match!("@behaviour GenServer", "@behaviour mod")
      assert Map.has_key?(caps, :mod)
    end

    test "@impl true" do
      assert {:ok, %{}} = match!("@impl true", "@impl true")
    end

    test "captures attribute name" do
      assert {:ok, caps} =
               match!("@env Application.get_env(:app, :key)", "@name Application.get_env(_, _)")

      assert caps[:name] == :env
    end

    test "wildcards in attribute name" do
      assert {:ok, %{}} =
               match!("@env Application.get_env(:app, :key)", "@_ Application.get_env(_, _)")
    end

    test "wildcard-prefixed attribute name" do
      assert {:ok, %{}} =
               match!("@env Application.get_env(:app, :key)", "@_name Application.get_env(_, _)")
    end

    test "literal attribute name match" do
      assert {:ok, caps} =
               match!("@env Application.get_env(:app, :key)", "@env Application.get_env(_, _)")

      assert caps[:env] == :env
    end

    test "captures attribute name with Patcher.find_all" do
      source = """
      @env Application.get_env(:my_app, :key)
      @timeout 5000
      @db_url Application.get_env(:my_app, :db_url)
      """

      alias ExAST.Patcher
      matches = Patcher.find_all(source, "@name Application.get_env(_, _)")
      assert length(matches) == 2
      assert Enum.map(matches, & &1.captures[:name]) == [:env, :db_url]
    end
  end

  describe "control flow" do
    test "case" do
      assert {:ok, %{}} =
               match!(
                 "case x do :ok -> 1 end",
                 "case _ do _ -> _ end"
               )
    end

    test "anonymous function" do
      assert {:ok, %{}} = match!("fn x -> x + 1 end", "fn _ -> _ end")
    end

    test "capture operator" do
      assert {:ok, %{}} = match!("&String.upcase/1", "&_/1")
    end
  end

  describe "explain/1" do
    test "reports signature, captures, wildcards, and callees" do
      output = Pattern.explain("Enum.map(coll, _)")
      assert output =~ "signature:  {:call, :map, 2}"
      assert output =~ "remote call Enum.map, arity 2"
      assert output =~ "coll — capture"
      assert output =~ "_ — wildcard"
    end

    test "describes local calls and attributes" do
      assert Pattern.explain("foo(a, b)") =~ "local call foo, arity 2"
      assert Pattern.explain("@attr _") =~ "attribute @attr"
    end

    test "renders a multi-statement block as a statement sequence, not a wildcard call" do
      output = Pattern.explain("x = get(_, _); delete(x)")

      assert output =~ "multi-node: true"
      assert output =~ "sequence of 2 statement(s):"
      assert output =~ "local call get, arity 2"
      assert output =~ "local call delete, arity 1"
      refute output =~ "wildcard local call (any name)"
    end

    test "renders a module alias by name, not as a wildcard call over segment literals" do
      output = Pattern.explain("%App.Auth.Context{flag: value}")

      assert output =~ "module App.Auth.Context"
      assert output =~ "value — capture"
      refute output =~ "wildcard local call (any name)"
      refute output =~ "literal :App"
    end

    test "describes with-expressions, wildcard callees, and repeated captures" do
      with_out = Pattern.explain("with {:ok, _} <- _ do ... end")
      assert with_out =~ "local call with, arity 2"
      assert with_out =~ "local call <-, arity 2"
      assert with_out =~ "... — ellipsis"

      any_call = Pattern.explain("_._(...)")
      assert any_call =~ "remote call _ (any)._ (any function), arity any (...)"

      unified = Pattern.explain("fun(x, x)")
      assert unified =~ "local call fun, arity 2"
      assert Regex.scan(~r/x — capture/, unified) |> length() == 2
    end

    test "shows the original pattern and its normalized parse in the header" do
      output = Pattern.explain("x |> foo() |> bar()")

      assert output =~ "pattern:    \"x |> foo() |> bar()\""
      assert output =~ "parsed:     bar(foo(x))"
    end

    test "flags a broad pattern in the header" do
      assert Pattern.explain("_") =~ "broad?:     true"
      assert Pattern.explain("Enum.map(coll, _)") =~ "broad?:     false"
    end

    test "lists the high-signal retrieval terms, hiding low-signal noise" do
      output = Pattern.explain("Enum.map(coll, _)")

      assert output =~ "terms:      "
      assert output =~ "call.remote:Enum.map/2"
      refute output =~ "node:call"
      refute output =~ "call.arity:2"
    end

    test "notes when a pattern has no high-signal terms to retrieve on" do
      assert Pattern.explain("42") =~
               "terms:      (none — retrieval falls back to the signature)"
    end

    test "accepts a quoted pattern and matches the string form's output" do
      from_string = Pattern.explain("IO.inspect(expr)")
      from_quoted = Pattern.explain(quote(do: IO.inspect(expr)))
      from_compiled = Pattern.explain(Pattern.compile("IO.inspect(expr)"))

      assert from_quoted =~ "remote call IO.inspect, arity 1"
      assert from_quoted =~ "expr — capture"

      # Only the `pattern:` header line differs by input form (inspected source
      # string vs. AST reconstruction); everything below it must be identical.
      drop_pattern_line = fn text -> text |> String.split("\n", parts: 2) |> List.last() end

      assert drop_pattern_line.(from_quoted) == drop_pattern_line.(from_string)
      assert drop_pattern_line.(from_compiled) == drop_pattern_line.(from_string)
    end

    test "renders a def head and its variable-arity body" do
      output = Pattern.explain("def handle(_, _) do ... end")

      assert output =~ "def definition, head:"
      assert output =~ "local call handle, arity 2"
      assert output =~ "... — ellipsis"
    end

    test "reports variable arity for ellipsis args" do
      output = Pattern.explain("foo(first, ...)")

      assert output =~ "local call foo, arity any (...)"
      assert output =~ "first — capture"
      assert output =~ "... — ellipsis"
    end

    test "renders a function capture's arity constraint" do
      output = Pattern.explain("&Enum.map/2")

      assert output =~ "local call &, arity 1"
      assert output =~ "arity-constrained head: name=Enum.map(), arity=2"
    end
  end

  describe "substitute/2" do
    test "replaces capture variables in template" do
      captures = %{expr: {:data, nil, nil}}
      template = Code.string_to_quoted!("Logger.debug(inspect(expr))")
      result = Pattern.substitute(template, captures)
      assert Macro.to_string(result) == "Logger.debug(inspect(data))"
    end

    test "leaves non-capture variables unchanged" do
      captures = %{expr: {:data, nil, nil}}
      template = Code.string_to_quoted!("IO.puts(other)")
      result = Pattern.substitute(template, captures)
      assert Macro.to_string(result) == "IO.puts(other)"
    end

    test "leaves wildcards unchanged" do
      captures = %{expr: {:data, nil, nil}}
      template = Code.string_to_quoted!("fn _ -> expr end")
      result = Pattern.substitute(template, captures)
      assert Macro.to_string(result) =~ "data"
    end
  end

  describe "multi_node?/1" do
    test "single expression" do
      refute Pattern.multi_node?("IO.inspect(x)")
    end

    test "semicolon-separated" do
      assert Pattern.multi_node?("a = 1; b = 2")
    end

    test "newline-separated" do
      assert Pattern.multi_node?("a = 1\nb = 2")
    end
  end

  describe "match_sequences/2" do
    test "finds contiguous match" do
      nodes = Enum.map(["x = 1", "y = 2", "z = 3"], &Code.string_to_quoted!/1)
      patterns = Enum.map(["_ = 1", "_ = 2"], &Code.string_to_quoted!/1)

      assert [{caps, 0..1}] = Pattern.match_sequences(nodes, patterns)
      assert map_size(caps) == 0
    end

    test "consistent captures across nodes" do
      nodes =
        Enum.map(
          ["a = Repo.get!(User, 1)", "Repo.delete(a)"],
          &Code.string_to_quoted!/1
        )

      patterns =
        Enum.map(
          ["x = Repo.get!(_, _)", "Repo.delete(x)"],
          &Code.string_to_quoted!/1
        )

      assert [{caps, 0..1}] = Pattern.match_sequences(nodes, patterns)
      assert Map.has_key?(caps, :x)
    end

    test "rejects inconsistent captures" do
      nodes =
        Enum.map(
          ["a = Repo.get!(User, 1)", "Repo.delete(b)"],
          &Code.string_to_quoted!/1
        )

      patterns =
        Enum.map(
          ["x = Repo.get!(_, _)", "Repo.delete(x)"],
          &Code.string_to_quoted!/1
        )

      assert [] = Pattern.match_sequences(nodes, patterns)
    end

    test "no match returns empty list" do
      nodes = Enum.map(["x = 1"], &Code.string_to_quoted!/1)
      patterns = Enum.map(["_ = 1", "_ = 2"], &Code.string_to_quoted!/1)

      assert [] = Pattern.match_sequences(nodes, patterns)
    end
  end

  describe "ellipsis (...)" do
    test "matches zero args" do
      assert {:ok, %{}} = match!("foo()", "foo(...)")
    end

    test "matches one arg" do
      assert {:ok, %{}} = match!("foo(1)", "foo(...)")
    end

    test "matches multiple args" do
      assert {:ok, %{}} = match!("foo(1, 2, 3)", "foo(...)")
    end

    test "captures before ellipsis" do
      assert {:ok, %{first: _}} = match!("foo(1, 2, 3)", "foo(first, ...)")
    end

    test "captures after ellipsis" do
      assert {:ok, %{last: _}} = match!("foo(1, 2, 3)", "foo(..., last)")
    end

    test "captures both sides of ellipsis" do
      assert {:ok, caps} = match!("foo(1, 2, 3, 4)", "foo(first, ..., last)")
      assert Map.has_key?(caps, :first)
      assert Map.has_key?(caps, :last)
    end

    test "rejects too few args for surrounding captures" do
      assert :error = match!("foo(1)", "foo(first, ..., last)")
    end

    test "ellipsis in list" do
      assert {:ok, %{}} = match!("[1, 2, 3]", "[...]")
    end

    test "ellipsis with head in list" do
      assert {:ok, %{head: _}} = match!("[1, 2, 3]", "[head, ...]")
    end

    test "ellipsis with tail in list" do
      assert {:ok, %{last: _}} = match!("[1, 2, 3]", "[..., last]")
    end

    test "matches remote call any arity" do
      assert {:ok, %{}} = match!("Enum.map(list, fun)", "Enum.map(...)")
    end

    test "matches remote call with capture + ellipsis" do
      assert {:ok, %{list: _}} = match!("Enum.reduce(list, acc, fun)", "Enum.reduce(list, ...)")
    end

    test "ellipsis in do block" do
      assert {:ok, %{}} = match!("def foo do\n  1\n  2\n  3\nend", "def foo do ... end")
    end

    test "ellipsis in case clause" do
      assert {:ok, %{}} = match!("case x do\n  :ok -> 1\nend", "case _ do ... end")
    end

    test "no match when ellipsis pattern head mismatches" do
      assert :error = match!("bar(1, 2)", "foo(...)")
    end

    test "quoted ellipsis" do
      ast = Sourceror.parse_string!("foo(1, 2, 3)")
      assert {:ok, %{}} = Pattern.match(ast, quote(do: foo(...)))
    end

    test "quoted ellipsis with capture" do
      ast = Sourceror.parse_string!("foo(1, 2, 3)")
      assert {:ok, %{first: _}} = Pattern.match(ast, quote(do: foo(first, ...)))
    end
  end

  describe "~p sigil" do
    import ExAST.Sigil

    test "parses pattern at compile time" do
      pattern = ~p"IO.inspect(_)"
      ast = Sourceror.parse_string!("IO.inspect(data)")
      assert {:ok, %{}} = Pattern.match(ast, pattern)
    end

    test "captures work" do
      pattern = ~p"Enum.map(list, fun)"
      ast = Sourceror.parse_string!("Enum.map(data, &to_string/1)")
      assert {:ok, caps} = Pattern.match(ast, pattern)
      assert Map.has_key?(caps, :list)
      assert Map.has_key?(caps, :fun)
    end

    test "ellipsis in sigil" do
      pattern = ~p"foo(first, ...)"
      ast = Sourceror.parse_string!("foo(1, 2, 3)")
      assert {:ok, %{first: _}} = Pattern.match(ast, pattern)
    end

    test "works with find_all" do
      source = """
      IO.inspect(a)
      IO.puts("hello")
      IO.inspect(b, label: "x")
      """

      matches = ExAST.Patcher.find_all(source, ~p"IO.inspect(...)")
      assert length(matches) == 2
    end

    test "works with replace_all" do
      source = "dbg(data)\n"
      result = ExAST.Patcher.replace_all(source, ~p"dbg(expr)", ~p"expr")
      assert result =~ "data"
      refute result =~ "dbg"
    end
  end
end
