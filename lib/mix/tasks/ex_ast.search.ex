defmodule Mix.Tasks.ExAst.Search do
  @shortdoc "Search Elixir code by AST pattern"
  @moduledoc """
  Searches for AST patterns in Elixir source files.

  ## Usage

      mix ex_ast.search 'IO.inspect(_)' [path ...]

  ## Options

    * `--count` — only print the number of matches
    * `--count-by-file` — print per-file match counts, most matches first
    * `--limit n` — stop after returning this many matches
    * `--allow-broad` — allow unbounded broad searches like `_`
    * `--expand-imports` — resolve bare `import Mod` (and `import Mod,
      except: [...]`) to the module's real exports, so `map(a, b)` matches
      `Mod.map(_, _)`. Requires `Mod` to be compiled and loadable.
    * `--inside 'pattern'` — only match inside ancestors matching this pattern
    * `--not-inside 'pattern'` — reject matches inside ancestors matching this pattern
    * `--parent 'pattern'` / `--not-parent 'pattern'` — filter by direct semantic parent
    * `--ancestor 'pattern'` / `--not-ancestor 'pattern'` — filter by semantic ancestor
    * `--has-child 'pattern'` / `--not-has-child 'pattern'` — filter by direct semantic child
    * `--contains 'pattern'` / `--not-contains 'pattern'` — filter by semantic descendant
    * `--has-descendant 'pattern'` / `--not-has-descendant 'pattern'` — aliases for contains filters
    * `--has 'pattern'` / `--not-has 'pattern'` — aliases for contains filters
    * `--follows 'pattern'` / `--not-follows 'pattern'` — filter by earlier sibling
    * `--precedes 'pattern'` / `--not-precedes 'pattern'` — filter by later sibling
    * `--immediately-follows 'pattern'` / `--not-immediately-follows 'pattern'` — filter by previous sibling
    * `--immediately-precedes 'pattern'` / `--not-immediately-precedes 'pattern'` — filter by next sibling
    * `--first` / `--not-first`, `--last` / `--not-last`, `--nth n` / `--not-nth n` — filter by sibling position
    * `--comment text` / `--not-comment text` — filter by associated comments
    * `--comment-before text`, `--comment-after text`, `--comment-inside text`, `--comment-inline text` — filter by comment location

    Comment values are substrings by default. Use `/.../` or `~r/.../` for regexes, including flags like `/todo/i`.

  ## Pattern syntax

  Patterns are valid Elixir expressions:

    * Variables (`name`, `expr`) — capture any node
    * `_` or `_name` — wildcard (match, don't capture)
    * Structs/maps — partial match (only listed keys must be present)
    * Pipes are normalized — `data |> Enum.map(f)` matches `Enum.map(data, f)`
    * Everything else — literal match

  ## Examples

      mix ex_ast.search 'IO.inspect(_)'
      mix ex_ast.search '%Step{id: "subject"}' lib/documents/
      mix ex_ast.search '{:error, reason}' lib/ test/
      mix ex_ast.search --count 'dbg(_)'
      mix ex_ast.search --inside 'def handle_call(_, _, _) do _ end' 'Repo.get!(_)'
      mix ex_ast.search --not-inside 'test _ do _ end' 'IO.inspect(_)'
      mix ex_ast.search 'IO.inspect(_)' --parent 'def _ do ... end'
      mix ex_ast.search 'def name do ... end' --contains 'Repo.transaction(_)' --not-contains 'IO.inspect(...)'
      mix ex_ast.search 'Repo.delete(record)' --follows 'record = Repo.get!(_, _)'
      mix ex_ast.search 'def name do ... end' --comment-inside TODO
      mix ex_ast.search 'def name do ... end' --comment-inside '/TODO|FIXME/'
      mix ex_ast.search '_' lib/ --limit 100
      mix ex_ast.search 'Enum.map(_, _)' lib/ --expand-imports
  """

  use Mix.Task

  alias ExAST.CLI.JSON
  alias ExAST.CLI.Output
  alias ExAST.CLI.SelectorOptions

  @impl Mix.Task
  def run(args) do
    {opts, positional, _} =
      OptionParser.parse(args,
        strict:
          [
            count: :boolean,
            count_by_file: :boolean,
            limit: :integer,
            allow_broad: :boolean,
            format: :string,
            json: :boolean,
            expand_imports: :boolean
          ] ++
            SelectorOptions.switches()
      )

    case positional do
      [pattern | paths] ->
        paths = if paths == [], do: ["lib/"], else: paths
        do_search(paths, pattern, opts)

      _ ->
        Mix.raise("Usage: mix ex_ast.search 'pattern' [path ...]")
    end
  end

  defp do_search(paths, pattern, opts) do
    validate_pattern!(pattern)

    search_pattern =
      SelectorOptions.pattern(pattern, opts, &validate_pattern!/1, [
        :count,
        :count_by_file,
        :limit,
        :allow_broad
      ])

    search_opts =
      opts
      |> SelectorOptions.where_opts([
        :count,
        :count_by_file,
        :limit,
        :allow_broad,
        :format,
        :json
      ])
      |> Keyword.merge(Keyword.take(opts, [:limit, :allow_broad, :expand_imports]))

    results = ExAST.search(paths, search_pattern, search_opts)

    Output.with_stdout(fn ->
      cond do
        json?(opts) ->
          JSON.print(%{matches: results, count: length(results)})

        opts[:count_by_file] ->
          print_count_by_file(results)

        opts[:count] ->
          Output.puts(length(results))

        true ->
          Enum.each(results, &print_match/1)
          Output.puts("\n#{length(results)} match(es)")
      end
    end)
  end

  defp validate_pattern!(pattern) do
    Code.string_to_quoted!(pattern)
  rescue
    e in [SyntaxError, TokenMissingError, MismatchedDelimiterError] ->
      Mix.raise("Invalid pattern: #{Exception.message(e)}")
  end

  defp print_count_by_file(results) do
    counts =
      results
      |> Enum.frequencies_by(& &1.file)
      |> Enum.sort_by(fn {_file, count} -> -count end)

    Enum.each(counts, fn {file, count} -> Output.puts("#{count}\t#{file}") end)
    Output.puts("\n#{length(results)} match(es) in #{length(counts)} file(s)")
  end

  defp json?(opts), do: opts[:json] || opts[:format] == "json"

  defp print_match(%{file: file, line: line, source: source, captures: captures}) do
    Output.puts("#{file}:#{line}")
    source |> String.split("\n") |> Enum.each(&Output.puts("  #{&1}"))
    print_captures(captures)
    Output.puts("")
  end

  defp print_captures(captures) when map_size(captures) == 0, do: :ok

  defp print_captures(captures) do
    for {name, value} <- captures do
      rendered = value |> restore_meta() |> Macro.to_string()
      Output.puts("  #{name}: #{rendered}")
    end
  end

  defp restore_meta(ast) do
    Macro.prewalk(ast, fn
      {form, nil, args} -> {form, [], args}
      other -> other
    end)
  end
end
